apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        applicationId "jp.webpay.android.token.sample"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 1
        versionName '1.0'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    lintOptions {
        disable 'IconMissingDensityFolder' // decided not to provide
        warningsAsErrors true
    }

    if (project.properties.containsKey("storeFile")) {
        signingConfigs {
            releaseConfig {
                // if you are a release manager, set these properties in gradle.properties
                storeFile = file(project.properties.storeFile)
                storePassword = project.properties.storePassword
                keyAlias = project.properties.keyAlias
                keyPassword = project.properties.keyPassword
            }
        }

        buildTypes {
            release {
                signingConfig signingConfigs.releaseConfig
            }
        }
    }
}

dependencies {
    //noinspection GradleDynamicVersion
    compile 'com.android.support:appcompat-v7:21.0.+'

    // Use this line on your own project instead of next line
    // compile 'jp.webpay.android:webpay-token:1.0.0@aar'
    compile project(':webpay-token')

    compile fileTree(dir: 'libs', include: ['*.jar'])
}
