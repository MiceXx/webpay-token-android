apply plugin: 'com.android.library'
apply plugin: 'robolectric'

description = 'Android Library for creating a WebPay token from a credit-card'
group = 'jp.webpay.android'
version = '1.0.0'

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 1
        versionName project.version
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    sourceSets {
        androidTest {
            setRoot('src/test')
        }
    }

    lintOptions {
        disable 'RtlHardcoded', 'RtlEnabled', // supports 8+, which does not provide RTL
                'IconMissingDensityFolder' // decided not to provide
        warningsAsErrors true
    }
}

dependencies {
    //noinspection GradleDependency
    compile 'com.android.support:appcompat-v7:[21.0.0,)'
    //noinspection GradleDependency
    compile 'com.android.support:support-v4:[21.0.0,)'

    androidTestCompile('org.robolectric:robolectric:2.4') {
      exclude group: 'commons-logging', module: 'commons-logging'
      exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    androidTestCompile 'junit:junit:4.11'
    androidTestCompile 'org.hamcrest:hamcrest-all:1.3'
}

if (gradle.startParameter.taskNames.contains('uploadArchives')) {
    apply from: 'build.publish.gradle'
}
